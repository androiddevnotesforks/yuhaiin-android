apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return 5
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return (((new Date().getTime() / 1000) - 1451606400) / 10).toString()
    }
}

def getCommit = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return "-" + stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}


android {
    compileSdk 33

    compileOptions {
        // Flag to enable support for the new language APIs

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "11"
    }

    defaultConfig {
        applicationId "io.github.asutorufa.yuhaiin"
        def documentsAuthorityValue = applicationId + ".documents"

        // Now we can use ${documentsAuthority} in our Manifest
        manifestPlaceholders =
                [documentsAuthority: documentsAuthorityValue]
        // Now we can use BuildConfig.DOCUMENTS_AUTHORITY in our code
        buildConfigField "String",
                "DOCUMENTS_AUTHORITY",
                "\"${documentsAuthorityValue}\""

        minSdk 21
        targetSdk 33

        versionCode getVersionCode()
        versionName getVersionName() + getCommit()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner" // 追加

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    splits {
        abi {
            enable true

            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()

            include "x86_64", "arm64-v8a"
        }
    }

    buildFeatures {
        compose false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.includeAndroidResources = true

        unitTests.all {
            systemProperty 'robolectric.dependency.repo.url', 'https://maven.aliyun.com/repository/public'
            systemProperty 'robolectric.dependency.repo.id', 'aliyunmaven'
        }

        namespace 'io.github.asutorufa.yuhaiin'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation 'com.google.android.material:material:1.6.1'
//    implementation "androidx.activity:activity-compose:1.5.0"
    implementation "androidx.browser:browser:1.4.0"

    // room
    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation 'com.google.code.gson:gson:2.9.1'

    // nav
    def nav_version = "2.5.1"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation project(":yuhaiin")
    implementation project(":logcatviewer")
    implementation project(":preferencex-simplemenu")


    testImplementation "androidx.test:core:1.4.0"
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestUtil 'androidx.test:orchestrator:1.4.1'
    testImplementation 'org.robolectric:robolectric:4.8.1'
}
